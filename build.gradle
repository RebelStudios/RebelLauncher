apply plugin: 'java'
apply plugin: 'groovy'

configurations {
    antTask
}

repositories {
    flatDir dirs: "${rootDir}/lib"
    mavenCentral()
}

dependencies {
    compile 'commons-io:commons-io:2.4'
    antTask name: 'javac2', version: '12.1.0'
    antTask name: 'forms_rt', version: '12.1.0'
    antTask name: 'asm4-all', version: '12.1.0-idea'
    antTask group: 'org.jdom', name: 'jdom', version: '1.1'
}

task compileJava(overwrite: true, dependsOn: configurations.compile.getTaskDependencyFromProjectDependency(true, 'jar')) {
    doLast {
        project.sourceSets.main.output.classesDir.mkdirs()
        ant.taskdef name: 'javac2', classname: 'com.intellij.ant.Javac2', classpath: configurations.antTask.asPath
        ant.javac2 srcdir: project.sourceSets.main.java.srcDirs.join(':'),
                classpath: project.sourceSets.main.compileClasspath.asPath,
                destdir: project.sourceSets.main.output.classesDir,
                source: sourceCompatibility,
                target: targetCompatibility,
                includeAntRuntime: false
    }
}